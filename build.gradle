apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'eclipse'
apply plugin: 'idea'

buildscript {
    final javaHome = System.env['JAVA_HOME']
    if (javaHome) {
        ext.javafxJar = "${javaHome}/jre/lib/jfxrt.jar"
    }

    try {
        println "JavaFX runtime jar: ${ext.javafxJar}"
        dependencies {
            classpath files(ext.javafxJar)
        }
    } catch (MissingPropertyException mpe) {
        println """
                 |  Please set the environment variable JAVA_HOME
                 |  to the directory that contains jre/lib/jfxrt.jar
                 |  of JavaFX version ${jfxtras_requiredJavaFxVersion}.\n""".stripMargin() 
        System.exit 1
    }
}

repositories {
    mavenRepo urls: "https://oss.sonatype.org/content/repositories/snapshots/"
    mavenCentral()
}

dependencies {
    compile     files(ext.javafxJar, "lib/jsr-310-ri-0.6.3.jar", "lib/jsr-310-TZDB-all-0.6.3.jar")
    testCompile  "junit:junit:${jfxtras_junitVersion}","org.mockito:mockito-all:1.9.5"
    testCompile  "net.java.jemmy:JemmyFX:0.9.3-SNAPSHOT"
}

apply {
    from 'gradle/mavenCentralDeploy.gradle'
    from 'http://gradle-plugins.mihosoft.eu/latest/vlicenseheader.gradle'
}

repairHeaders.licenseHeaderText = new File(projectDir,"license-template.txt")

task buildTimestamped(type: Copy, dependsOn: 'build') {
    from 'build/libs'
    into 'build/libs/timestamped'
    include '*.jar'
    rename { String fileName ->
        fileName.replace('SNAPSHOT', "SNAPSHOT-${new Date().format('HH-mm-dd-MM-yyyy')}")
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.0-rc-3'
}

signArchives.onlyIf {
    project.hasProperty('signing.keyId')
}

uploadArchives.onlyIf {
    project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')
}

task copyToLib(type: Copy) {
    into "_lib"
    from configurations.testCompile
}
